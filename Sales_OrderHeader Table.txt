WITH SalesOrderDetailCTE AS (
	SELECT 
		sod.[SalesOrderID],
		sod.[SalesOrderDetailID],
		sod.[OrderQty],
		sod.[ProductID],
		sod.[SpecialOfferID],
		sod.[UnitPrice],
		sod.[UnitPriceDiscount],
		sod.[LineTotal],
		p.[Name] AS ProductName,
		p.[ProductNumber],
		pm.[Name] AS ProductModelName,
		p.[Color],
		p.[SafetyStockLevel],
		p.[StandardCost],
		p.[ListPrice],
		pc.[Name] AS ProductCategoryName,
		ps.[Name] AS ProductSubcategoryName
	FROM 
		[AdventureWorks2022].[Sales].[SalesOrderDetail] AS sod
	-- Join with Product table
	LEFT JOIN 
		[AdventureWorks2022].[Production].[Product] AS p
		ON sod.[ProductID] = p.[ProductID]
	-- Join with ProductSubcategory table
	LEFT JOIN 
		[AdventureWorks2022].[Production].[ProductSubcategory] AS ps
		ON p.[ProductSubcategoryID] = ps.[ProductSubcategoryID]
	-- Join with ProductCategory table
	LEFT JOIN 
		[AdventureWorks2022].[Production].[ProductCategory] AS pc
		ON ps.[ProductCategoryID] = pc.[ProductCategoryID]
	-- Join with ProductModel table
	LEFT JOIN [AdventureWorks2022].[Production].[ProductModel] AS pm
		ON p.[ProductModelID] = pm.[ProductModelID]
)

-- Main query with RIGHT JOIN
SELECT
    soh.[SalesOrderID],
    -- Extract day, month, year from OrderDate
    DAY(soh.[OrderDate]) AS [OrderDay],
    MONTH(soh.[OrderDate]) AS [OrderMonth],
    YEAR(soh.[OrderDate]) AS [OrderYear],
    DATENAME(MONTH, soh.[OrderDate]) AS [OrderMonthName], -- Month name for OrderDate

    -- Extract day, month, year from DueDate
    DAY(soh.[DueDate]) AS [DueDay],
    MONTH(soh.[DueDate]) AS [DueMonth],
    YEAR(soh.[DueDate]) AS [DueYear],
    DATENAME(MONTH, soh.[DueDate]) AS [DueMonthName], -- Month name for DueDate

    -- Extract day, month, year from ShipDate
    DAY(soh.[ShipDate]) AS [ShipDay],
    MONTH(soh.[ShipDate]) AS [ShipMonth],
    YEAR(soh.[ShipDate]) AS [ShipYear],
    DATENAME(MONTH, soh.[ShipDate]) AS [ShipMonthName], -- Month name for ShipDate

    soh.[SalesOrderNumber],
    soh.[PurchaseOrderNumber],
    soh.[CustomerID],
    soh.[SalesPersonID],
    soh.[BillToAddressID],
    soh.[ShipToAddressID],
    sm.[Name] AS [ShipMethodName],
    cc.[CardType],
    soh.[CurrencyRateID],
    soh.[SubTotal],
    soh.[TaxAmt],
    soh.[Freight],
    soh.[TotalDue],
    st.[Name] AS [TerritoryName],
    st.[Group] AS [TerritoryGroup],
    
    -- Concatenate all sales reasons into one string
    STRING_AGG(sr.[Name], ', ') AS SalesReasonNames,
    
    -- Additional fields from SalesOrderDetailCTE
    sodcte.ProductName,
    sodcte.ProductNumber,
    sodcte.ProductModelName,
    sodcte.Color,
    sodcte.SafetyStockLevel,
    sodcte.StandardCost,
    sodcte.ListPrice,
    sodcte.ProductCategoryName,
    sodcte.ProductSubcategoryName
FROM [AdventureWorks2022].[Sales].[SalesOrderHeader] AS soh
    
RIGHT JOIN 
    SalesOrderDetailCTE AS sodcte
    ON soh.[SalesOrderID] = sodcte.[SalesOrderID] 
LEFT JOIN 
    [AdventureWorks2022].[Sales].[SalesTerritory] AS st
    ON soh.[TerritoryID] = st.[TerritoryID]
LEFT JOIN 
    [AdventureWorks2022].[Purchasing].[ShipMethod] AS sm
    ON soh.[ShipMethodID] = sm.[ShipMethodID]
LEFT JOIN
    [AdventureWorks2022].[Sales].[SalesOrderHeaderSalesReason] AS sohsr
    ON soh.[SalesOrderID] = sohsr.[SalesOrderID]
LEFT JOIN
    [AdventureWorks2022].[Sales].[SalesReason] AS sr
    ON sohsr.[SalesReasonID] = sr.[SalesReasonID]
LEFT JOIN
    [AdventureWorks2022].[Sales].[CreditCard] AS cc
    ON soh.[CreditCardID] = cc.[CreditCardID]
GROUP BY
    soh.[SalesOrderID],
    soh.[OrderDate],
    soh.[DueDate],
    soh.[ShipDate],
    soh.[SalesOrderNumber],
    soh.[PurchaseOrderNumber],
    soh.[CustomerID],
    soh.[SalesPersonID],
    soh.[BillToAddressID],
    soh.[ShipToAddressID],
    sm.[Name],
    cc.[CardType],
    soh.[CurrencyRateID],
    soh.[SubTotal],
    soh.[TaxAmt],
    soh.[Freight],
    soh.[TotalDue],
    st.[Name],
    st.[Group],
    sodcte.ProductName,
    sodcte.ProductNumber,
    sodcte.ProductModelName,
    sodcte.Color,
    sodcte.SafetyStockLevel,
    sodcte.StandardCost,
    sodcte.ListPrice,
    sodcte.ProductCategoryName,
    sodcte.ProductSubcategoryName;